buildscript {
  ext {
    buildToolsVersion = '30.0.3'
    ndkVersion = '20.1.5948944'
    minSdkVersion = 21
    compileSdkVersion = 29
    targetSdkVersion = 29
  }

  repositories {
    google()
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.1'
    classpath 'com.google.gms:google-services:4.3.8'
  }
}

allprojects {
  task downloadDependencies() {
    description 'Download all dependencies to the Gradle cache'
  }

  repositories {
    google()
    mavenLocal()
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url("$rootDir/../node_modules/react-native/android")
    }
    maven {
      // Android JSC is installed from npm
      url("$rootDir/../node_modules/jsc-android/dist")
    }
    maven {
      url "$rootDir/../node_modules/detox/Detox-android"
    }
    maven {
      url "$rootDir/../node_modules/@notifee/react-native/android/libs"
    }
    jcenter()
  }

}

subprojects {
  task listAllDependencies(type: DependencyReportTask) {}
  ext {
    compileSdk = rootProject.ext.compileSdkVersion
    buildTools = rootProject.ext.buildToolsVersion
    minSdk = rootProject.ext.minSdkVersion
    targetSdk = rootProject.ext.targetSdkVersion
  }

  // Set up all other sub-projects - from other react-native modules, to use our versions
  afterEvaluate { project ->
    if (!project.name.equalsIgnoreCase('app') && project.hasProperty('android')) {
      android {
        compileSdkVersion compileSdk
        buildToolsVersion buildTools
        defaultConfig {
          minSdkVersion minSdk
          targetSdkVersion targetSdk
        }
      }
    }
  }
}

