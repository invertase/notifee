buildscript {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.1'
  }
}

apply plugin: 'com.android.library'

android {
  compileSdkVersion 30
  testOptions {
    unitTests.returnDefaultValues = true
  }
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 30
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'consumer-rules.pro'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [eventBusIndex: 'app.notifee.core.EventBusIndex']
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      debuggable false
      jniDebuggable false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

android.libraryVariants.all { variant ->
  def variantName = variant.name.capitalize()
  def assembleTaskName = "assemble${variantName}"
  def copyTaskName = "copy${variantName}Artifacts"

  task(copyTaskName, type: Copy, dependsOn: assembleTaskName, group: "build") {
    variant.outputs.each { output ->
      def newOutputName = "notifee_core_" + output.name + ".aar"
      from(output.outputFile.parent) {
        include output.outputFile.name
        rename output.outputFile.name, newOutputName
      }
    }
    into new File("$project.rootDir/../packages/react-native/android/libs")
  }

  project.tasks.getByName(assembleTaskName).finalizedBy(copyTaskName)
}

repositories {
  google()
  jcenter()
  mavenCentral()
}

dependencies {

  api 'androidx.annotation:annotation:1.1.0' // https://developer.android.com/jetpack/androidx/releases/annotation
  api "com.squareup.okhttp3:okhttp:3.12.12" // okhttp must stay on 3.12.x to support minSdkVersion < 21
  api 'androidx.concurrent:concurrent-futures:1.1.0' // https://developer.android.com/jetpack/androidx/releases/concurrent
  api 'com.google.android.gms:play-services-tasks:17.2.0' // https://developers.google.com/android/guides/releases
  api 'androidx.work:work-runtime:2.4.0' // https://developer.android.com/jetpack/androidx/releases/work
  // Fresco 2.3.0+ may not be react-native compatible until https://github.com/facebook/react-native/pull/30061
  api 'com.facebook.fresco:fresco:2.2.0' // https://github.com/facebook/fresco/releases
  api 'io.jsonwebtoken:jjwt-api:0.11.2' // https://github.com/jwtk/jjwt/releases

  def room_version = '2.2.6' // https://developer.android.com/jetpack/androidx/releases/room
  implementation "androidx.room:room-runtime:$room_version"
  annotationProcessor "androidx.room:room-compiler:$room_version"

  def eventbus_version = '3.2.0' // https://github.com/greenrobot/EventBus/releases
  api "org.greenrobot:eventbus:$eventbus_version"
  annotationProcessor "org.greenrobot:eventbus-annotation-processor:$eventbus_version"

  testImplementation 'junit:junit:4.13.1' // https://github.com/junit-team/junit4/releases
  androidTestImplementation 'androidx.test.ext:junit:1.1.2' // https://developer.android.com/jetpack/androidx/releases/test
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0' // see above

}
