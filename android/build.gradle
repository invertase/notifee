buildscript {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.3'
  }
}

apply plugin: 'com.android.library'

android {
  compileSdkVersion 29
  buildToolsVersion "29.0.2"
  testOptions {
    unitTests.returnDefaultValues = true
  }
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'consumer-rules.pro'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [eventBusIndex: 'app.notifee.core.EventBusIndex']
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      debuggable false
      jniDebuggable false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

android.libraryVariants.all { variant ->
  def variantName = variant.name.capitalize()
  def assembleTaskName = "assemble${variantName}"
  def copyTaskName = "copy${variantName}Artifacts"

  task(copyTaskName, type: Copy, dependsOn: assembleTaskName, group: "build") {
    variant.outputs.each { output ->
      def newOutputName = "notifee_core_" + output.name + ".aar"
      from(output.outputFile.parent) {
        include output.outputFile.name
        rename output.outputFile.name, newOutputName
      }
    }
    into new File("$project.rootDir/../packages/react-native/android/libs")
  }

  project.tasks.getByName(assembleTaskName).finalizedBy(copyTaskName)
}

repositories {
  google()
  jcenter()
  mavenCentral()
}

dependencies {
  api "com.squareup.okhttp3:okhttp:+"
  api 'androidx.concurrent:concurrent-futures:1.0.0'
  api 'com.google.android.gms:play-services-tasks:17.1.0'
  api 'androidx.work:work-runtime:2.4.0'
  api 'org.greenrobot:eventbus:3.1.1'
  api 'com.facebook.fresco:fresco:2.1.0'
  api 'io.jsonwebtoken:jjwt-api:0.10.7'

  annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
