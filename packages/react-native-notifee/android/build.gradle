buildscript {
  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      jcenter()
    }

    dependencies {
      classpath("com.android.tools.build:gradle:3.6.3")
    }
  }
}

plugins {
  id "io.invertase.gradle.build" version "1.3"
}

project.ext {
  set('react-native', [
    versions: [
      android: [
        minSdk    : 16,
        targetSdk : 29,
        compileSdk: 29,
        // optional as gradle.buildTools comes with one by default
        // overriding here though to match the version RN uses
        // buildTools: "29.0.2"
      ],
    ],
  ])
}

android {
  defaultConfig {
    multiDexEnabled true
  }
  lintOptions {
    disable 'GradleCompatible'
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  buildTypes {
    release {
      consumerProguardFiles 'proguard-rules.pro'
    }
  }
  compileSdkVersion = 29
  buildToolsVersion = '29.0.2'
}

repositories {
  google()
  jcenter()
}

dependencies {
  if (findProject(':notifee_core')) {
    implementation findProject(':notifee_core')
  } else {
    implementation fileTree(include: ['notifee_core_release.aar'], dir: 'libs')
  }
  implementation 'androidx.concurrent:concurrent-futures:1.0.0'
  implementation 'com.google.android.gms:play-services-tasks:17.1.0'
  implementation 'androidx.work:work-runtime:2.3.4'
  implementation 'org.greenrobot:eventbus:3.1.1'

  api 'io.jsonwebtoken:jjwt-api:0.10.7'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.10.7'
  runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.10.7') {
    exclude group: 'org.json', module: 'json'
  }
}

ReactNative.shared.applyPackageVersion()
ReactNative.shared.applyDefaultExcludes()
ReactNative.module.applyAndroidVersions()
ReactNative.module.applyReactNativeDependency("api")
apply from: file('./notifee-json.gradle')
